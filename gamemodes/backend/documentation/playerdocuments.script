/***
 *
 *  ##     ##    ###    ########  ##    ## ##          ###    ##    ## ########  
 *  ###   ###   ## ##   ##     ##  ##  ##  ##         ## ##   ###   ## ##     ## 
 *  #### ####  ##   ##  ##     ##   ####   ##        ##   ##  ####  ## ##     ## 
 *  ## ### ## ##     ## ########     ##    ##       ##     ## ## ## ## ##     ## 
 *  ##     ## ######### ##   ##      ##    ##       ######### ##  #### ##     ## 
 *  ##     ## ##     ## ##    ##     ##    ##       ##     ## ##   ### ##     ## 
 *  ##     ## ##     ## ##     ##    ##    ######## ##     ## ##    ## ########   
 *
 *  @Author         Vostic
 *  @Date           05th May 2023
 *  @Weburl         https://maryland-ogc.com
 *  @Project        maryland_project
 *
 *  @File           playerdocuments.script
 *  @Module         documentation
 */

#include <ysilib\YSI_Coding\y_hooks>

hook OnGameModeInit()
{
    print("documentation/playerdocuments.script loaded");

    return Y_HOOKS_CONTINUE_RETURN_1;
}

//      >> [ VARIABLES ] <<


//>>
enum PlayerDocumentsz
{
    pNationalID,
    pPassport,
    pDriveLicense,
    pMotoLicense,
    pBoatLicense,
    pGunLicense
}
new PlayerDocuments[MAX_PLAYERS][PlayerDocumentsz];

hook OnPlayerConnect(playerid) {

    // Documents of player
    PlayerDocuments[playerid][pNationalID] = 0;
    PlayerDocuments[playerid][pPassport] = 0;
    PlayerDocuments[playerid][pDriveLicense] = 0;
    PlayerDocuments[playerid][pMotoLicense] = 0;
    PlayerDocuments[playerid][pBoatLicense] = 0;
    PlayerDocuments[playerid][pGunLicense] = 0;

    return Y_HOOKS_CONTINUE_RETURN_1;
}

hook OnCharacterLoaded(playerid)
{   
    new q[267];
    mysql_format(SQL, q, sizeof(q), "SELECT * FROM player_documents WHERE character_document = '%d' LIMIT 1", GetCharacterSQLID(playerid));
    mysql_tquery(SQL, q, "SQL_AccountDocumentsLoad", "i", playerid);

	return 1;
}


//
YCMD:getidcard(playerid, params[], help)
{

    if(IsPlayerInRangeOfPoint(playerid, 3.0, 1505.4467,1337.0447,10.9788))
    {
        if(PlayerDocuments[playerid][pNationalID] == 1) return notification.Show(playerid, "GRESKA", "Vec imate licnu kartu", "!", BOXCOLOR_RED);
        if(GetPlayerMoney(playerid) < 200) return notification.Show(playerid, "GRESKA", "Nemate dovoljno novca", "!", BOXCOLOR_RED);

        PlayerDocuments[playerid][pNationalID] = 1;
        // - Dodati kada se odradi player_currency
        // GiveMoneyEx(playerid, -200);
        ApplyAnimation(playerid, "DEALER", "shop_pay", 4.1, false, false, false, false, 0);
        notification.Show(playerid, "OPSTINA", "Uspesno si izvadio licnu kartu!", "!", BOXCOLOR_GREEN);

        SaveDocuments(playerid);
    }
    else notification.Show(playerid, "GRESKA", "Morate biti na salteru u opstini", "!", BOXCOLOR_RED);

    return Y_HOOKS_CONTINUE_RETURN_1;

}

YCMD:getpassport(playerid, params[], help)
{
    if(IsPlayerInRangeOfPoint(playerid, 3.0, 1505.4467,1337.0447,10.9788))
    {
        if(PlayerDocuments[playerid][pPassport] == 1) return notification.Show(playerid, "GRESKA", "Vec imate pasos", "!", BOXCOLOR_RED);
        if(GetPlayerMoney(playerid) < 200) return notification.Show(playerid, "GRESKA", "Nemate dovoljno novca", "!", BOXCOLOR_RED);

        PlayerDocuments[playerid][pPassport] = 1;
        
        // - Dodati kada se odradi player_currency
        // GiveMoneyEx(playerid, -200);
        ApplyAnimation(playerid, "DEALER", "shop_pay", 4.1, false, false, false, false, 0);
        notification.Show(playerid, "OPSTINA", "Uspesno si izvadio pasos!", "!", BOXCOLOR_GREEN); // Jesi alahami

        SaveDocuments(playerid);
    }
    else notification.Show(playerid, "GRESKA", "Morate biti na salteru u opstini", "!", BOXCOLOR_RED);

    return Y_HOOKS_CONTINUE_RETURN_1;

}

// @alias(sl, showid);
YCMD:pokazilicnu(playerid, params[], help) = showid;
YCMD:showid(playerid, params[], help)
{
    if(help)
    {
        notification.Show(playerid, "HELP", "Pokazite licnu kartu drugom igracu", "+", BOXCOLOR_BLUE);
        return 1;
    }

    // Check if player has National ID
    if(PlayerDocuments[playerid][pNationalID] == 0)
        return SendClientMessage(playerid, x_server, "maryland \187; "c_white"Nemate licnu kartu!");

    // Get target player
    new targetid;
    if(sscanf(params, "u", targetid))
        return SendClientMessage(playerid, x_server, "maryland \187; "c_white"/showid [id/ime]");

    // Basic checks
    if(!IsPlayerConnected(targetid))
        return SendClientMessage(playerid, x_server, "maryland \187; "c_white"Taj igrac nije konektovan!");
    
    if(targetid == playerid)
        return SendClientMessage(playerid, x_server, "maryland \187; "c_white"Ne mozete pokazati licnu kartu sami sebi!");

    // Distance check (3 meters)
    if(!DistanceBetweenPlayers(3.0, playerid, targetid))
        return SendClientMessage(playerid, x_server, "maryland \187; "c_white"Morate biti blize igracu!");

    // Show ID information to target player
    new string[256];
    format(string, sizeof(string), "maryland \187; "c_white"Licna Karta\n\
        Ime i Prezime: %s\n\
        Godine: %d\n\
        Skin ID: %d\n\
        Drzava: Los Santos, San Andreas",
        ReturnCharacterName(playerid),
        CharacterInfo[playerid][Age],
        GetPlayerSkin(playerid)
    );
    SendClientMessage(targetid, x_server, string);

    // Notify the player showing the ID
    SendClientMessage(playerid, x_server, "maryland \187; "c_white"Pokazali ste licnu kartu.");

    return 1;
}

/*
    * Funkcije:
*/
forward SQL_AccountDocumentsLoad(playerid);
public SQL_AccountDocumentsLoad(playerid)
{
    static rows;
    cache_get_row_count(rows);
    if(!rows) 
    {
        new q[300];
        mysql_format(SQL, q, sizeof(q), 
           "INSERT INTO `player_documents` (character_document, NationalID, Passport, DriveLicense, MotoLicense, BoatLicense, GunLicense) \ 
            VALUES('%d', '0', '0', '0', '0', '0', '0')", GetCharacterSQLID(playerid));
        mysql_tquery(SQL, q);
    }
    else 
    {
        cache_get_value_name_int(0, "NationalID", PlayerDocuments[playerid][pNationalID]);
        cache_get_value_name_int(0, "Passport", PlayerDocuments[playerid][pPassport]);
        cache_get_value_name_int(0, "DriveLicense", PlayerDocuments[playerid][pDriveLicense]);
        cache_get_value_name_int(0, "MotoLicense", PlayerDocuments[playerid][pMotoLicense]);
        cache_get_value_name_int(0, "BoatLicense", PlayerDocuments[playerid][pBoatLicense]);
        cache_get_value_name_int(0, "GunLicense", PlayerDocuments[playerid][pGunLicense]);
    }
}