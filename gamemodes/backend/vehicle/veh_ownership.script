/***
 *
 *  ##     ##    ###    ########  ##    ## ##          ###    ##    ## ########  
 *  ###   ###   ## ##   ##     ##  ##  ##  ##         ## ##   ###   ## ##     ## 
 *  #### ####  ##   ##  ##     ##   ####   ##        ##   ##  ####  ## ##     ## 
 *  ## ### ## ##     ## ########     ##    ##       ##     ## ## ## ## ##     ## 
 *  ##     ## ######### ##   ##      ##    ##       ######### ##  #### ##     ## 
 *  ##     ## ##     ## ##    ##     ##    ##       ##     ## ##   ### ##     ## 
 *  ##     ## ##     ## ##     ##    ##    ######## ##     ## ##    ## ########   
 *
 *  @Author         Vostic & Ogy_
 *  @Date           05th May 2023
 *  @Weburl         https://maryland-ogc.com
 *  @Project        maryland_project
 *
 *  @File           vehicle_ownership.script
 *  @Module         vehicle
 */

#include <ysilib\YSI_Coding\y_hooks>

#define VEHICLE_OWNER_NONE			(0)

const MAX_CARS = 2000;
const MAX_PLATE_LEN = 32;

enum {

	FUEL_TYPE_PETROL = 1,
	FUEL_TYPE_DIESEL,
	FUEL_TYPE_METAN,
	FUEL_TYPE_ELECTRIC
}

enum e_VEHICLE_DATA {

	vID,
	vOwner,
	vModel,
	vColor[2], 
	vPlate[MAX_PLATE_LEN],

	Float:vPos[4],

	vRegDate,
	vOil,
	vRange,
	vRangeKM,
 
	vFuel,
	vFuelType,		
	bool:vAlarm

}

new eVehicle[MAX_CARS][e_VEHICLE_DATA],
	pvVehicle[MAX_CARS],

	Iterator:iter_Vehicles<MAX_CARS>;


#include "backend/stocks/vehicle.stock"								//* Vehicle Stock Provere


forward LoadVehicleData();
public LoadVehicleData() {

	new rows = cache_num_rows();
	if(!rows) return false;
	else {

		for(new i = 0; i < rows; i++) {

			cache_get_value_name_int(i, "vID", eVehicle[i][vID]);
			cache_get_value_name_int(i, "vOwner", eVehicle[i][vOwner]);
			cache_get_value_name_int(i, "vModel", eVehicle[i][vModel]);
			cache_get_value_name_int(i, "Color1", eVehicle[i][vColor][0]);
			cache_get_value_name_int(i, "Color2", eVehicle[i][vColor][1]);

			cache_get_value_name(i, "vPlate", eVehicle[i][vPlate], MAX_PLATE_LEN);

			cache_get_value_name_float(i, "vPosX", eVehicle[i][vPos][0]);
			cache_get_value_name_float(i, "vPosY", eVehicle[i][vPos][1]);
			cache_get_value_name_float(i, "vPosZ", eVehicle[i][vPos][2]);
			cache_get_value_name_float(i, "vPosA", eVehicle[i][vPos][3]);

			cache_get_value_name_int(i, "vRegDate", eVehicle[i][vRegDate]);
			cache_get_value_name_int(i, "vOil", eVehicle[i][vOil]);
			cache_get_value_name_int(i, "vRange", eVehicle[i][vRange]);
			cache_get_value_name_int(i, "vRangeKM", eVehicle[i][vRangeKM]);

			cache_get_value_name_int(i, "vFuel", eVehicle[i][vFuel]);
			cache_get_value_name_int(i, "vFuelType", eVehicle[i][vFuelType]);
			cache_get_value_name_bool(i, "vAlarm", eVehicle[i][vAlarm]);

			pvVehicle[i] = CreateVehicle(eVehicle[i][vModel], eVehicle[i][vPos][0], eVehicle[i][vPos][1], eVehicle[i][vPos][2], eVehicle[i][vPos][3], eVehicle[i][vColor][0], eVehicle[i][vColor][1], 1500, true);

			new plate_string[MAX_PLATE_LEN];
			format(plate_string, sizeof plate_string, "%s", eVehicle[i][vPlate]);

			SetVehicleNumberPlate(pvVehicle[i], plate_string);

			Iter_Add(iter_Vehicles, i);

			printf("LOADED : Vehicle %d * Owner %d * VehicleID %d", eVehicle[i][vID], eVehicle[i][vOwner], pvVehicle[i]);

		}

		printf("Ucitano %d vozila", rows);

	}


	return 1;
}

forward CreateVehicleData(id);
public CreateVehicleData(id) {

	eVehicle[id][vID] = cache_insert_id();
	pvVehicle[id] = CreateVehicle(eVehicle[id][vModel], eVehicle[id][vPos][0], eVehicle[id][vPos][1], eVehicle[id][vPos][2]+4.0, eVehicle[id][vPos][3], eVehicle[id][vColor][0], eVehicle[id][vColor][1], 1500, true);

	new plt_tmp[MAX_PLATE_LEN];
	format(plt_tmp, sizeof plt_tmp, "%s", eVehicle[id][vPlate]);

	SetVehicleNumberPlate(pvVehicle[id], plt_tmp);

	Iter_Add(iter_Vehicles, id);
	return 1;
}


hook OnGameModeInit() {

	Iter_Init(iter_Vehicles);

	mysql_tquery(SQL, "SELECT * FROM `vehicles`", "LoadVehicleData");

	return 1;
}

YCMD:createvehicle(playerid, params[], help) {

	if(!IsPlayerAdmin(playerid))
		return notification.Show(playerid, "Error", "Samo RCON Admini.", "!", BOXCOLOR_RED);

	new model, color[2];

	if(sscanf(params, "ddd", model, color[0], color[1]))
		return notification.Show(playerid, "Usage", "/createvehicle [Model] [Boja 1] [Boja 2]", "?", BOXCOLOR_GREEN);

	new Float:pPos[4], id = Iter_Free(iter_Vehicles);

	new plate_str[MAX_PLATE_LEN];
	format(plate_str, sizeof plate_str, "N/A-00-12");

	eVehicle[id][vOwner] = 0;
	eVehicle[id][vModel] = model;
	eVehicle[id][vColor][0] = color[0];
	eVehicle[id][vColor][1] = color[1];

	GetPlayerPos(playerid, pPos[0], pPos[1], pPos[2]);
	GetPlayerFacingAngle(playerid, pPos[3]);

	eVehicle[id][vPos][0] = pPos[0];
	eVehicle[id][vPos][1] = pPos[1];
	eVehicle[id][vPos][2] = pPos[2];
	eVehicle[id][vPos][3] = pPos[3];

	strmid(eVehicle[id][vPlate], plate_str, 0, strlen(plate_str), MAX_PLATE_LEN);

	eVehicle[id][vRegDate] = 0;
	eVehicle[id][vOil] = 100;
	eVehicle[id][vRange] = 0;
	eVehicle[id][vRangeKM] = 0;

	eVehicle[id][vFuel] = 100;

	if(PetroleumVehicle(id)) { eVehicle[id][vFuelType] = FUEL_TYPE_PETROL; }
	else if(DieselVehicle(id)) { eVehicle[id][vFuelType] = FUEL_TYPE_DIESEL; }
	else if(MetanVehicle(id)) { eVehicle[id][vFuelType] = FUEL_TYPE_METAN; }
	else if(ElectricVehicle(id)) { eVehicle[id][vFuelType] = FUEL_TYPE_ELECTRIC; }

	eVehicle[id][vAlarm] = false;

	new q[666];

	mysql_format(SQL, q, sizeof q, "INSERT INTO `vehicles` (`vOwner`, `vModel`, `Color1`, `Color2`, `vPlate`, \
								    `vPosX`, `vPosY`, `vPosZ`, `vPosA`, `vRegDate`, `vOil`, `vRange`, `vRangeKM`, `vFuel`, `vFuelType`, `vAlarm`) \
									VALUES ('0', '%d', '%d', '%d', '%e', '%f', '%f', '%f', '%f', '0', '100', '0', '0', '100', '%d', '0')",
									eVehicle[id][vModel], eVehicle[id][vColor][0], eVehicle[id][vColor][1],
									eVehicle[id][vPlate], eVehicle[id][vPos][0], eVehicle[id][vPos][1],
									eVehicle[id][vPos][2], eVehicle[id][vPos][3], eVehicle[id][vFuelType]);
	mysql_tquery(SQL, q, "CreateVehicleData", "d", id);

	return 1;
}

/*

	? Note : ne uzimati nikakve ideje od nejtana;
	? Note : ne uzimati nikakve IDEJE od NEJTANA v2;
	? NOTE : NE UZIMATI NIKAKVE IDEJE OD NEJTANA v3!!!!!!!!!!!; ZAPAMTITI ZAOKRUZITI ISPRINTATI UOKVIRITI!

*/