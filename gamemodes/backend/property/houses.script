/***
 *
 *  ##     ##    ###    ########  ##    ## ##          ###    ##    ## ########  
 *  ###   ###   ## ##   ##     ##  ##  ##  ##         ## ##   ###   ## ##     ## 
 *  #### ####  ##   ##  ##     ##   ####   ##        ##   ##  ####  ## ##     ## 
 *  ## ### ## ##     ## ########     ##    ##       ##     ## ## ## ## ##     ## 
 *  ##     ## ######### ##   ##      ##    ##       ######### ##  #### ##     ## 
 *  ##     ## ##     ## ##    ##     ##    ##       ##     ## ##   ### ##     ## 
 *  ##     ## ##     ## ##     ##    ##    ######## ##     ## ##    ## ########   
 *
 *  @Author         Vostic & Ogy_
 *  @Date           24th May 2023
 *  @Weburl         https://maryland-ogc.com
 *  @Project        maryland_project
 *
 *  @File           houses.script
 *  @Module         property
 */


//==================={Includes}=========================
#include <ysilib\YSI_Coding\y_hooks>
//======================================================

#define MAX_DYNAMIC_HOUSES		(9999)

new KreiraKucaID[MAX_PLAYERS],
	inProperty[MAX_PLAYERS],
	OdabraoKucu[MAX_PLAYERS],
	KreiraTypeID[MAX_PLAYERS];

enum H_House 
{
	houseID,
	bool:houseExists,
	houseOwner,
	houseAdd[50],
	housePrice,
	houseType,
	Float:housePos[4],
	Float:housePosInt[4],
	houseInterior,
	houseInteriorVW,
	houseExteriorVW,
	houseExterior,
	bool:houseLocked,
	//
	Text3D:houseLabel,
	housePickup,
	houseMapIcon
};
new HouseData[MAX_DYNAMIC_HOUSES][H_House];
new Iterator:Kuce<MAX_DYNAMIC_HOUSES>;

enum player_property {

    HouseID
}
new PlayerProperty[MAX_PLAYERS][player_property];


hook OnGameModeInit()
{
	mysql_pquery(SQL, "SELECT * FROM `houses`", "House_Load" );
	return (true);
}

Dialog:dialog_kreirajkucu(playerid, response, listitem, inputtext[])
{
	if(!response) return 1;
	if(response)
	{
		new id;
		id = Iter_Free(Kuce);
		if(id == -1)
			return SendClientMessage(playerid, x_red, "Prekoracen je limit vaseg iteratora.");

		if(id == 0)
		{
			Iter_Add(Kuce, 0);	
		}

		KreiraKucaID[playerid] = id;

		HouseData[id][houseType] = listitem+1;
		KreiraTypeID[playerid] = HouseData[id][houseType];

		Dialog_Show(playerid, dialog_kreirajkucucena, DIALOG_STYLE_INPUT, "Maryland - Kucu Kreiraj:", "Upisite zeljenu cenu za kucu koju pravite.\n\n** CENA NE SME BITI ISPOD 1$ a iznad 50.000.000$", "Dalje", "Izlaz");
	}
	return (true);
}
Dialog:dialog_kreirajkucucena(playerid, response, listitem, inputtext[])
{
	if(!response) return 1;
	if(response)
	{
		new id = KreiraKucaID[playerid];
		new type = KreiraTypeID[playerid];
		new Float:Pozicija[4];
		new kolicina;
		if(sscanf(inputtext, "d", kolicina))
			return Dialog_Show(playerid, dialog_kreirajkucucena, DIALOG_STYLE_INPUT, "Maryland - Kucu Kreiraj:", "Upisite zeljenu cenu za kucu koju pravite.\n\n** CENA NE SME BITI ISPOD 1$ a iznad 50.000.000$", "Dalje", "Izlaz");

		if(kolicina < 1 || kolicina > 50000000) 
			return SendClientMessage(playerid, x_red, "Iznos cene ove kuce ne moze biti ispod 1 a iznad 50.000.000$"); 

		if(HouseData[id][houseType] < 3 && kolicina >= 5000000)
			return SendClientMessage(playerid, x_red, "Kako mislis da stavis za Malu/Veliku kucu vecu cenu od 5.000.000$ ????");

		HouseData[id][housePrice] = kolicina;
		GetPlayerPos(playerid, Pozicija[0], Pozicija[1], Pozicija[2]);
		GetPlayerFacingAngle(playerid, Pozicija[3]);

		House_Create(playerid, Pozicija[0], Pozicija[1], Pozicija[2], Pozicija[3], kolicina, type);

		SendClientMessage(playerid, x_server, "Uspesno ste kreirali kucu.");
	}
	return (true);
}

Dialog:dialog_kucaodabir(playerid, response, listitem, inputtext[])
{
	if(!response) return OdabraoKucu[playerid] = -1;
	if(response)
	{
		switch(listitem)
		{
			case 0:
			{
				new id = OdabraoKucu[playerid];
				if(inProperty[playerid] == -1)
					return SendClientMessage(playerid, x_red, "Niste u kuci.");

				new zakljucan[30];
				if(HouseData[id][houseLocked] == false) zakljucan = "{00ff2a}Otkljucano"; else zakljucan = "{ff0000}Zakljucano";
				new string[200];
				format(string,sizeof(string),"{65FEAF}Informacije o kuci\n\t{ffffff}Vlasnik Kuce: %s\n\tID Kuce: %d\n\tStatus vrata: %s\n\t{ffffff}Cena kuce: %s",ReturnPlayerName(playerid),id,zakljucan,FormatNumber(HouseData[id][housePrice]));
				Dialog_Show(playerid, dialog_none, DIALOG_STYLE_MSGBOX, "Informacije o kuci", string, "Nazad", "");
				OdabraoKucu[playerid] = -1;
			}
			case 1:
			{
				new id = OdabraoKucu[playerid];

				if(IsPlayerInRangeOfPoint(playerid, 2.0, HouseData[id][housePos][0],HouseData[id][housePos][1],HouseData[id][housePos][2]) || inProperty[playerid] == id)
				{
					if (!HouseData[id][houseLocked])
					{
						HouseData[id][houseLocked] = true;
						House_Save(id);

						SendClientMessage(playerid, x_server, "Uspesno ste zakljucali vasu kucu.");
						PlayerPlaySound(playerid, 1145, 0.0, 0.0, 0.0);
						OdabraoKucu[playerid] = -1;
					}
					else
					{
						HouseData[id][houseLocked] = false;
						House_Save(id);

						SendClientMessage(playerid, x_server, "Uspesno ste otkljucali vasu kucu.");
						PlayerPlaySound(playerid, 1145, 0.0, 0.0, 0.0);
						OdabraoKucu[playerid] = -1;
					}
				}

			}
			case 2 .. 4:
			{
				return SendClientMessage(playerid, x_red, "Trenutno nedostupno.");
			}
		}
	}
	return (true);
}

hook OnPlayerKeyStateChange(playerid, newkeys, oldkeys)
{
	if( newkeys == KEY_SECONDARY_ATTACK )
	{
		new entrance_finded = false;
		if( !entrance_finded )
		{
			foreach(new i : Kuce)
			{
				if(IsPlayerInRangeOfPoint( playerid, 2.0, HouseData[ i ][ housePos ][ 0 ], HouseData[ i ][ housePos ][ 1 ], HouseData[ i ][ housePos ][ 2 ] ) )
				{
					if( HouseData[ i ][ houseLocked ] == false)
					{
						SetPlayerPos( playerid, HouseData[ i ][ housePosInt ][ 0 ], HouseData[ i ][ housePosInt ][ 1 ], HouseData[ i ][ housePosInt ][ 2 ] );
						UcitajIgracuObjekte( playerid );
						SetCameraBehindPlayer( playerid );
						SetPlayerInterior( playerid, HouseData[ i ][ houseInterior ] );
						SetPlayerVirtualWorld( playerid, HouseData[ i ][ houseInteriorVW ] );
						SetPlayerTime( playerid, 12, 0 );
						inProperty[ playerid ] = i;

						entrance_finded = true;
						break;
					}
					else return GameTextForPlayer( playerid, "~r~Zakljucano", 5000, 6 );
				}
				else if( IsPlayerInRangeOfPoint( playerid, 2.0, HouseData[ i ][ housePosInt ][ 0 ], HouseData[ i ][ housePosInt ][ 1 ], HouseData[ i ][ housePosInt ][ 2 ] ) && GetPlayerVirtualWorld( playerid ) == HouseData[ i ][ houseInteriorVW ] )
				{
					SetPlayerPos( playerid, HouseData[ i ][ housePos ][ 0 ], HouseData[ i ][ housePos ][ 1 ], HouseData[ i ][ housePos ][ 2 ] );
					UcitajIgracuObjekte( playerid );
					SetCameraBehindPlayer( playerid );
					SetPlayerInterior( playerid, 0);
					SetPlayerVirtualWorld( playerid, 0);
					//SetPlayerTime( playerid, ServerInfo[ VremeInGame ], 0 );
					inProperty[ playerid ] = -1;

					entrance_finded = true;
					break;
				}
			}
		}
	}
	return (true);
}

forward House_Load();
public House_Load()
{
   	new iRows, id;
	iRows = cache_num_rows();
	for(new i = 0; i < iRows; i++)
	{
		cache_get_value_name_int(i, "SQLID", id);
		HouseData[id][houseExists] = true;
	    cache_get_value_name_int(i, "OwnerID", HouseData[id][houseOwner]);
		cache_get_value_name(i, "Address", HouseData[id][houseAdd], 50);
	    cache_get_value_name_int(i, "Price", HouseData[id][housePrice]);
	    cache_get_value_name_int(i, "Type", HouseData[id][houseType]);
	    cache_get_value_name_float(i, "PosX", HouseData[id][housePos][0]);
	    cache_get_value_name_float(i, "PosY", HouseData[id][housePos][1]);
	    cache_get_value_name_float(i, "PosZ", HouseData[id][housePos][2]);
	    cache_get_value_name_float(i, "PosA", HouseData[id][housePos][3]);
	    cache_get_value_name_float(i, "IntX", HouseData[id][housePosInt][0]);
	    cache_get_value_name_float(i, "IntY", HouseData[id][housePosInt][1]);
	    cache_get_value_name_float(i, "IntZ", HouseData[id][housePosInt][2]);
	    cache_get_value_name_float(i, "IntA", HouseData[id][housePosInt][3]);
	    cache_get_value_name_int(i, "InteriorID", HouseData[id][houseInterior]);
	    cache_get_value_name_int(i, "InteriorVw", HouseData[id][houseInteriorVW]);
	    cache_get_value_name_int(i, "ExteriorInt", HouseData[id][houseExterior]);
	    cache_get_value_name_int(i, "ExteriorVw", HouseData[id][houseExteriorVW]);
	    cache_get_value_name_bool(i, "Locked", HouseData[id][houseLocked]);
	    
	    Iter_Add(Kuce, 0);
	    Iter_Add(Kuce, id);
	    House_Label(id);
	}
	printf("Ucitavanje Kuca(%d)... Uspesno",iRows);
	return 1;
}

House_Save(houseid)
{
	new
	    query[650];

	format(query, sizeof(query), "UPDATE `houses` SET `OwnerID` = '%d', `Address`, `Price` = '%d', `Type` = '%d', `PosX` = '%f', `PosY` = '%f', `PosZ` = '%f', `PosA` = '%f', `IntX` = '%f', `IntY` = '%f', `IntZ` = '%f', `IntA` = '%f', `InteriorID` = '%d', `ExteriorInt` = '%d', `ExteriorVw` = '%d', `Locked` = '%d', `InteriorVw` = '%d' WHERE `SQLID` = '%d'",
	    HouseData[houseid][houseOwner],
		HouseData[houseid][houseAdd],
	    HouseData[houseid][housePrice],
	    HouseData[houseid][houseType],
	    HouseData[houseid][housePos][0],
	    HouseData[houseid][housePos][1],
	    HouseData[houseid][housePos][2],
	    HouseData[houseid][housePos][3],
	    HouseData[houseid][housePosInt][0],
	    HouseData[houseid][housePosInt][1],
	    HouseData[houseid][housePosInt][2],
	    HouseData[houseid][housePosInt][3],
        HouseData[houseid][houseInterior],
        HouseData[houseid][houseExterior],
        HouseData[houseid][houseExteriorVW],
        HouseData[houseid][houseLocked],
        HouseData[houseid][houseInteriorVW],
        houseid);
	return mysql_tquery(SQL, query);
}
House_Inside(playerid)
{
    for (new i = 0; i != MAX_DYNAMIC_HOUSES; i ++) if (HouseData[i][houseExists] && HouseData[i][houseID] == PlayerProperty[playerid][HouseID] && GetPlayerInterior(playerid) == HouseData[i][houseInterior] && GetPlayerVirtualWorld(playerid) == HouseData[i][houseInteriorVW]) {
        return i;
	}
	return -1;
}
House_Create(playerid, Float:Xx, Float:Yy, Float:Zz, Float:Angle, price, Type)
{
	for (new i = 1; i != MAX_DYNAMIC_HOUSES; i ++)
	{
    	if (!HouseData[i][houseExists])
	    {
	    	new id = KreiraKucaID[playerid];
	        HouseData[i][houseExists] = true;
    	    HouseData[i][houseOwner] = 0;
        	HouseData[i][housePrice] = price;

	        HouseData[i][housePos][0] = Xx;
	        HouseData[i][housePos][1] = Yy;
	        HouseData[i][housePos][2] = Zz;
	        HouseData[i][housePos][3] = Angle;
	        HouseData[i][houseType] = Type;

	        if(HouseData[i][houseType] == 1) // mala
	        {
	        	HouseData[i][housePosInt][0] = 223.0732;
	            HouseData[i][housePosInt][1] = 1288.3668;
	            HouseData[i][housePosInt][2] = 1082.1406;
	            HouseData[i][housePosInt][3] = 90.0000;
	            HouseData[i][houseInterior] = 1;
	        }
	        else if(HouseData[i][houseType] == 2) // velika
	        {
	        	HouseData[i][housePosInt][0] = 2317.8977;
	            HouseData[i][housePosInt][1] = -1025.7722;
	            HouseData[i][housePosInt][2] = 1050.2109;
	            HouseData[i][housePosInt][3] = 90.0000;	
	          	HouseData[i][houseInterior] = 9;        	
	        }
	        else if(HouseData[i][houseType] == 3) // vila
	        {
	        	HouseData[i][housePosInt][0] = 140.2605;
	            HouseData[i][housePosInt][1] = 1367.4221;
	            HouseData[i][housePosInt][2] = 1083.8615;
	            HouseData[i][housePosInt][3] = 90.0000;
	            HouseData[i][houseInterior] = 5;   
	        }
	        else if(HouseData[i][houseType] == 4) // donatorska
	        {
	        	HouseData[i][housePosInt][0] = 2269.8772;
	            HouseData[i][housePosInt][1] = -1210.3240;
	            HouseData[i][housePosInt][2] = 1047.5625;
	            HouseData[i][housePosInt][3] = 90.0000;
	            HouseData[i][houseInterior] = 10; 
	        }

			HouseData[i][houseInteriorVW] = id;
			HouseData[i][houseExterior] = GetPlayerInterior(playerid);
			HouseData[i][houseExteriorVW] = GetPlayerVirtualWorld(playerid);

			HouseData[i][houseLocked] = false;

			new zone[28], add[50];
			GetPlayer2DZone(playerid, zone, sizeof(zone));
			format(add, sizeof(add), "%d, %s, Maryland",id, zone);
			strmid(HouseData[i][houseAdd], add, 0, sizeof(add), 255);

			House_Label(i);
			static q[900];
			mysql_format(SQL, q, sizeof(q),
			    "INSERT INTO `houses` (`OwnerID`, `Address`, `Price`, `Type`, `PosX`, `PosY`, `PosZ`, `PosA`, `IntX`, `IntY`, `IntZ`, `IntA`, `InteriorVw`, `InteriorID`, `ExteriorInt`, `ExteriorVw`, `Locked`) \
				 VALUES('%d','%d','%d','%f','%f','%f','%f', '%f', '%f', '%f', '%f', '%d', '%d', '%d', '%d', '%d')", 
				 HouseData[i][houseOwner],add,HouseData[i][housePrice],HouseData[i][houseType],HouseData[i][housePos][0],HouseData[i][housePos][1],HouseData[i][housePos][2],HouseData[i][housePos][3],HouseData[i][housePosInt][0],HouseData[i][housePosInt][1],
				 HouseData[i][housePosInt][2],HouseData[i][housePosInt][3],HouseData[i][houseInteriorVW],HouseData[i][houseInterior], HouseData[i][houseExterior],HouseData[i][houseExteriorVW],HouseData[i][houseLocked]);

			mysql_pquery(SQL, q, "OnHouseCreated", "d", i);
			KreiraKucaID[playerid] = -1;
			KreiraTypeID[playerid] = -1;
			return i;
		}
	}
	return -1;
}

forward OnHouseCreated(houseid);
public OnHouseCreated(houseid)
{
	if (houseid == -1 || !HouseData[houseid][houseExists])
	    return 0;

	HouseData[houseid][houseID] = cache_insert_id();
	House_Save(houseid);
	Iter_Add(Kuce, houseid);
	return 1;
}
forward House_Label(id);
public House_Label(id)
{
	if (id != -1 && HouseData[id][houseExists])
	{
		if (IsValidDynamic3DTextLabel(HouseData[id][houseLabel]))
		    DestroyDynamic3DTextLabel(HouseData[id][houseLabel]);

		if (IsValidDynamicPickup(HouseData[id][housePickup]))
		    DestroyDynamicPickup(HouseData[id][housePickup]);

		if (IsValidDynamicMapIcon(HouseData[id][houseMapIcon]))
		    DestroyDynamicMapIcon(HouseData[id][houseMapIcon]);

		if(HouseData[id][houseOwner]) {
			new string[200];
			format( string, sizeof( string ), "   » {ffffff}KUCA[%d]{DC578C} «   \n{ffffff}Adresa:{DC578C} %s\n{ffffff}Tip Kuce:{DC578C} %s",id, HouseData[id][houseAdd], getKucaType(HouseData[id][houseType]));
			HouseData[id][houseLabel] = CreateDynamic3DTextLabel( string, 0xDC578CFF, HouseData[ id ][ housePos ][0], HouseData[ id ][ housePos ][1], HouseData[ id ][ housePos ][2], 25.0, INVALID_PLAYER_ID, INVALID_VEHICLE_ID, 0, HouseData[id][houseExterior], HouseData[id][houseExteriorVW] );
		}
		else
		{			
			new string[200];
			format( string, sizeof( string ), "   » {ffffff}KUCA[%d]{DC578C} «   \n{ffffff}Adresa:{DC578C} %s\n{ffffff}Tip Kuce:{DC578C} %s\n{ffffff}Cena Kuce:{DC578C} %d$\n{DC578C}'/kupikucu'",id, HouseData[id][houseAdd], getKucaType(HouseData[id][houseType]), HouseData[id][housePrice]);
			HouseData[id][houseLabel] = CreateDynamic3DTextLabel( string, 0xDC578CFF, HouseData[ id ][ housePos ][0], HouseData[ id ][ housePos ][1], HouseData[ id ][ housePos ][2], 25.0, INVALID_PLAYER_ID, INVALID_VEHICLE_ID, 0, HouseData[id][houseExterior], HouseData[id][houseExteriorVW] );
		}
		HouseData[id][housePickup] = CreateDynamicPickup( 1273, 1, HouseData[ id ][ housePos ][0], HouseData[ id ][ housePos ][1], HouseData[ id ][ housePos ][2]);
        HouseData[id][houseMapIcon] = CreateDynamicMapIcon(HouseData[id][housePos][0], HouseData[id][housePos][1], HouseData[id][housePos][2], (HouseData[id][houseOwner] != 0) ? (32) : (31), 0, HouseData[id][houseExterior], HouseData[id][houseExteriorVW] );
	}
	return (true);
}
House_Delete(houseid)
{
	if (houseid != -1 && HouseData[houseid][houseExists])
	{
	    new
	        string[64];

		format(string, sizeof(string), "DELETE FROM `houses` WHERE `SQLID` = '%d'", houseid);
		mysql_tquery(SQL, string);

        if (IsValidDynamic3DTextLabel(HouseData[houseid][houseLabel]))
		    DestroyDynamic3DTextLabel(HouseData[houseid][houseLabel]);

		if (IsValidDynamicPickup(HouseData[houseid][housePickup]))
		    DestroyDynamicPickup(HouseData[houseid][housePickup]);

		if (IsValidDynamicMapIcon(HouseData[houseid][houseMapIcon]))
		    DestroyDynamicMapIcon(HouseData[houseid][houseMapIcon]);

	    HouseData[houseid][houseExists] = false;
	    HouseData[houseid][houseOwner] = 0;
	    HouseData[houseid][houseID] = 0;
	}
	return 1;
}
House_GetCount(playerid)
{
	new
		count = 0;

	for (new i = 0; i != MAX_DYNAMIC_HOUSES; i ++)
	{
		if (HouseData[i][houseExists] && House_IsOwner(playerid, i))
   		{
   		    count++;
		}
	}
	return count;
}
House_IsOwner(playerid, houseid)
{
	if (!IgracUlogovan[playerid] || PlayerInfo[playerid][SQLID] == -1)
	    return 0;

    if ((HouseData[houseid][houseExists] && HouseData[houseid][houseOwner] != 0) && HouseData[houseid][houseOwner] == PlayerInfo[playerid][SQLID])
		return 1;

	return 0;
}
forward GetNearestHouse(playerid);
public GetNearestHouse(playerid)
{
	new Float:p_Pos[3], Float:maxradius = 3.0, id = -1;
	GetPlayerPos(playerid, p_Pos[0], p_Pos[1], p_Pos[2]);

    foreach(new i : Kuce)
    {
        if(VectorSize(p_Pos[0] - HouseData[i][housePos][0], p_Pos[1] - HouseData[i][housePos][1], p_Pos[2] - HouseData[i][housePos][2]) >= maxradius) continue;

        id = i;
        maxradius = VectorSize(p_Pos[0] - HouseData[i][housePos][0], p_Pos[1] - HouseData[i][housePos][1], p_Pos[2] - HouseData[i][housePos][2]);
    }
    return id;
}

getKucaType(id)
{
	new typezz[30] = "Nema Tipa";
	switch(id)
	{
		case 1: typezz = "Mala Kuca";
		case 2: typezz = "Velika Kuca";
		case 3: typezz = "Vila";
		case 4: typezz = "Donatorska Kuca";
	}
	return typezz;
}